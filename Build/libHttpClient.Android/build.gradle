apply plugin: "com.android.library"

android {
    compileSdkVersion 27
    ndkVersion "22.0.7026061"

    defaultConfig {
        targetSdkVersion 21
        minSdkVersion 21

        externalNativeBuild {
            cmake {
                if (project.hasProperty("HC_NOWEBSOCKETS")) {
                    arguments << "-DHC_NOWEBSOCKETS=1"
                }

                // externalNativeBuild normally skips static
                // lib targets. This forces it to build.
                targets "libHttpClient.Android"
            }
        }
    }

    externalNativeBuild {
        cmake {
            path "../../Utilities/CMake/Android/libHttpClient/CMakeLists.txt"
        }
    }

    compileOptions {
        sourceCompatibility=JavaVersion.VERSION_1_7
        targetCompatibility=JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt")
        }
    }

    // We want to copy the built Java `.aar` binary to the libHttpClient `Binaries` dir.
    libraryVariants.all { variant ->
         variant.outputs.each { output ->
             output.outputFileName = "../../../../../Binaries/Android/java/${variant.getBuildType().getName()}/libHttpClient.aar"
         }
    }
}

dependencies {
    implementation "com.squareup.okhttp3:okhttp:3.10.0"
}
